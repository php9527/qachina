可以在脚本文件头部中加入下面代码，通过 tmux 启动 CL程序 并将其转入后台执行。
#!/bin/sh
#|
tmux new-session -d -s session-name || exit 1
tmux send-keys -t session-name "ccl -l $0" C-m
exit
|#

这一方式通过 tmux 所提供的 send-keys 命令直接向终端模拟输入按键，从而可以向 CL程序 的 REPL 传递任意接口函数。示例如下，C-m 表示回车。
tmux send-keys -t session-name "(foo)" C-m

相对来说，这一方式更为灵活。同时如果需要调回交互终端，可以使用下面的命令：
tmux attach -t session-name

#!/bin/sh
#| -*- mode: lisp -*-
tmux kill-session -t volume-control
exec tmux new-session -d -s volume-control "LD_LIBRARY_PATH=$HOME/lib ccl -l $0"
|#

#-alsa-mixer
(progn
  (pushnew :alsa-mixer *features*)
  (open-shared-library "libasound.so")
  (open-shared-library "libalsavolume.so"))

#-libnotify
(progn
  (pushnew :libnotify *features*)
  (open-shared-library "libnotify.so")
  (with-cstrs ((progname "volume-control"))
    (external-call "notify_init" :address progname :int)))

(defun get-control-volume (control)
  (with-cstrs ((selem control))
    (external-call "alsa_mixer_get_volume" :address selem :int)))

(defun get-master-volume () (get-control-volume "Master"))
(defun get-pcm-volume () (get-control-volume "PCM"))

(defun set-control-volume (control vol)
  (with-cstrs ((selem control))
    (external-call "alsa_mixer_set_volume" :address selem :int vol)))

(defun set-master-volume (vol) (set-control-volume "Master" vol))
(defun set-pcm-volume (vol) (set-control-volume "PCM" vol))

(defun adjust-master-volume (delta)
  (set-master-volume (max 0 (min 100 (+ delta
					(get-master-volume))))))

(defun raise-volume () (adjust-master-volume 3))

(defun lower-volume () (adjust-master-volume -3))

(defun toggle-mute ()
  (set-pcm-volume (if (= (get-pcm-volume) 0) 100 0)))

(defun send-notify ()
  (let ((handle (with-cstrs
                    ((summary "Volume")
                     (body (format nil "Current: ~[mute~:;~a%~]"
                                   (get-pcm-volume) (get-master-volume)))
                     (icon "info"))
                  (external-call "notify_notification_new"
                                 :address summary
                                 :address body
                                 :address icon
                                 :address))))
    (external-call "notify_notification_set_timeout"
                   :address handle :int 500 :void)
    (external-call "notify_notification_show"
                   :address handle :address (%null-ptr) :int)))


(defun raise () (raise-volume) (send-notify))
(defun lower () (lower-volume) (send-notify))
(defun toggle () (toggle-mute) (send-notify))
